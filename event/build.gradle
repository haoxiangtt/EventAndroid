apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
//apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
//    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'
}


def BASENAME = "eventAndroid";
def sdkDestinationPath = "build/outputs/jar/";
android.libraryVariants.all { variant ->
    def finalName = "${BASENAME}-${variant.name}-v${variant.mergedFlavor.versionName}"
    def deleteBuild = tasks.create(name : "delete${variant.name}Jar",
            type : Delete) {
        delete "${sdkDestinationPath}${finalName}.jar"
    }
    tasks.create(name : "makeJar${variant.name.capitalize()}",
            dependsOn : [deleteBuild, variant.assemble],
            type : Jar) {
//        def zfile = file("build/intermediates/bundles/${variant.name}/classes.jar")
//        from zipTree(zfile)
        from "build/intermediates/classes/${variant.name}"
        baseName finalName
        destinationDir file(sdkDestinationPath)
        exclude "android/support/**/*.class"
        exclude "**/R.class"
        exclude "**/R\$*.class"
        include "event/**/*.class"
    }
}

def USER = 'jinfu123'
def KEY = 'de46da1a18c6adf29c142fb9fd3a6cde2cb35328'
def siteUrl = 'https://github.com/haoxiangtt/EventAndroid'      // 项目的主页
def gitUrl = 'https://github.com/haoxiangtt/EventAndroid.git'   // Git仓库的url
group = "com.bfy"                                        // Maven Group ID for the artifact，一般填你唯一的包名
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'this is a thread manage framework.'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'no id'     //填写的一些基本信息
                        name 'haoxiangtt'
                        email '20386450@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
bintray {
    user = USER
    key = KEY
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "EventAndroid"   //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
javadoc { //jav doc采用utf-8编码否则会报“GBK的不可映射字符”错误
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}
/*
publish {
    userOrg = 'jinfu123'//bintray.com用户名
    groupId = 'com.bfy'//jcenter上的路径
    artifactId = 'eventandroid'//项目名称
    publishVersion = '1.0.0'//版本号
    desc = 'this is a thread manage framework.'//描述，不重要
    website = 'https://github.com/haoxiangtt/EventAndroid'//网站，不重要；尽量模拟github上的地址，例如我这样的；当然你有地址最好了
}*/
